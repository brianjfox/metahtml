;;; color-gif.mhtml: -*- Meta-HTML -*-  Use Image library to color in GIF.
;;;
;;; Copyright (c) 2000 Brian J. Fox
;;; Author: Brian J. Fox (bfox@ai.mit.edu) Wed Dec  6 06:30:57 2000.

;;; Get the image loaded into our world.
<require coloring-book>
<cbook::initialize>
<image::create image src=<cbook::bookfile <get-var-once image>>>

;;; Color any points that need coloring.
<foreach point points>
  <set-var args[]=<subst-in-string <get-var-once point> "," "\n">>
  <image::fill image
	       x=<get-var-once args[0]>
	       y=<get-var-once args[1]>
	       color=<get-var-once args[2]>>
</foreach>

;;; Jump through a hoop to get the modified image in a standard variable.
<randomize>
<set-var fname = /tmp/image-<make-identifier 32>>
<image::write image <get-var-once fname>>
<dir::read-file <get-var-once fname> data>
<cgi-exec rm -f <get-var-once fname>>

;;; Write the image to the browser.
<with-open-stream s *standard-output* mode=write-create>
  <stream-put s "HTTP/1.0 200 OK\n">
  <stream-put s "Content-type: image/gif\n">
  <stream-put s "Content-length: <content-length data>\n\n">
  <stream-put-contents s data>
  <stream-shutdown s>
</with-open-stream>
