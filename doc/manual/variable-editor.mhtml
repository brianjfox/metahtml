;;; variable-editor.mhtml: -*- Meta-HTML -*-  DESCRIPTIVE TEXT.

;;;  Copyright (c) 1996 Brian J. Fox
;;;  Author: Brian J. Fox (bfox@ai.mit.edu) Fri Jul  5 12:23:30 1996.
<set-var title="Edit Package Variables">
<include header.mhtml>


<if <not <get-var name>>
    <set-var name="all">>

<when <get-var name>>
  <if <string-eq <get-var name> "all" caseless=true>
      <set-var packages[]=<package-names>>
    <set-var packages[]=<get-var name>>>
</when>

<center>
  <form method=post action="<thisdoc>">
    <b>Package: </b>
    <input type=text size=20 name=name value="<get-var name>">
  </form>
</center>
<p>

<defsubst make-table package>
  <b>Package Variables: <upcase <get-var package>><b></br>
  <set-var names[]=<downcase <package-vars <get-var package>>>>
  <sort names>
  <vertical-table default::names 2 create-fundoc.mhtml>
  <p>
</defsubst>

<set-var i=0>
<while <get-var packages[i]>>
  <if <and <not <string-eq <get-var packages[i]> "*META-HTML*">>
	   <not <string-eq <get-var packages[i]> "DEFAULT">>>
      <make-table <get-var packages[i]>>>
  <increment i>
</while>

<include footer.mhtml>
