#!/www/bin/mhc
;;; mkapi: -*- Meta-HTML -*- Build documentation API from sources.
;;;
;;;  Copyright (c) 1996 Brian J. Fox
;;;  Author: Brian J. Fox (bfox@ai.mit.edu) Wed May 27 19:56:11 1998
<set-var mhc::explicit-output-only=true>
<set-var mhtml::gather-documentation = true>
<set-var mhtml::include-prefix[] = <cgi-exec pwd>>

;;; Parse command line arguments.
;;; They are simply the name of the output file, and the names of files
;;; of Meta-HTML code to include, then write out documentation for.
<set-var filenames[]="" output-file="" this-arg-is-output="">

<foreach arg program-arguments start=1>
  <if <get-var this-arg-is-output>
      <set-var output-file=<get-var arg> this-arg-is-output="">
    <if <or <string-eq <get-var arg> -o>
	    <string-eq <get-var arg> --output>>
	<set-var this-arg-is-output=true>
      <array-append <get-var arg> filenames>>>
</foreach>

<copy-var *meta-html*::include *meta-html*::mhc-include>

<set-var mkapi::inclev = 0>

<defsubst include f &optional verbatim &key &unevalled alt whitespace=delete>
  <mhc::print
   <concat <pad " " <mul 2 mkapi::inclev> align=right>
	   " Including <get-var-once f>\n">>
  <increment mkapi::inclev>
  <mhc-include <get-var-once f> <get-var-once verbatim>
	       alt=<or <get-var-once alt>
		       <mhc::print " Failed!!\n">>>
  <decrement mkapi::inclev>
</defsubst>

<set-var mhtml::flag-newly-interned-symbols = t>
<foreach fname filenames>
  <if <string-eq <substring <get-var-once fname> 0 1> "/">
      <set-var mhtml::include-prefix = "/">>
  <include <get-var-once fname>>
</foreach>
<unset-var mhtml::flag-newly-interned-symbols>

<undef include>

;;; Get the list of functions that we have just read in.
<set-var all-funcs[]=<package-vars *user-functions* strip=true>>
<sort all-funcs>

;;; Write the documentation out to a file.
<set-var api-file = <dir::basename <get-var-once output-file>>>
<set-var mhtml-file = <concat
		       <dir::filename-sans-extension <get-var-once api-file>>
		       .mhtml>>

<set-var curr-prefix="">
<with-open-stream s <or <get-var output-file> *standard-output*>
  mode=write-create>
  <stream-put
   s
   "<get-var api-file>: An API in -*- Text -*- mode.
This file contains the Application Programmers Interface created by `mkapi'
for the tagset file <get-var mhtml-file> on <date>.
Do NOT edit this file, as it is automatically created.\n\n">
  <foreach fun all-funcs>
    <when <%%function-documentation <get-var fun>>>
      <set-var args[]= <%%function-arguments <get-var fun>>>
      <set-var doc = <%%function-documentation <get-var fun>>>
      <set-var type= <alist-get-var
		      <symbol-info *user-functions*::<get-var-once fun>>
		      TYPE>>
      <subst-in-var type "USER " "">
      <set-var prefix =
	<subst-in-string <get-var fun> "(^[^:]+)(::)(.*$)" "\\1">>

      ;;; See if this starts a new documentation section.
      <if <not <string-eq <get-var curr-prefix> <get-var prefix>>>
	  ;;; Yes.  Say the right thing.
	  <concat
	   <if <get-var-once prefix>
	       <concat
		<stream-put p "Functions in class <get-var prefix>:\n">
		<stream-put p <pad "" 60 align=center pad-char="-">>
		<stream-put p "\n">>>
	   <set-var curr-prefix = <get-var prefix>>>>

      <var-case
	type=DEFUN	<set-var class=DEFUN>
	type=SUBST	<set-var class=DEFUN>
	type=MACRO	<set-var class=DEFMACRO>>

      ;;; Put the opening line in the output file.
      <unset-var line>
      <set-var line = <downcase <get-var-once fun>>>
      ;;; Put the args.
      <foreach arg args>
	<set-var line = <concat <get-var-once line> " <get-var-once arg>">>
      </foreach>
      <set-var ws = <sub 72 <string-length <get-var line>>>>
      <set-var ws = <sub ws <string-length class>>>
      <when <lt ws 0>>
	<set-var line[]=<word-wrap <get-var-once line> width=60>>
	<set-var line[0]=<subst-in-string <get-var-once line[0]> "^ +" "">>
	<set-var ws = <sub 72 <string-length <get-var line>>>>
	<set-var ws = <sub ws <string-length class>>>
      </when>
      <if <lt ws 0> <set-var ws=1>>
      <stream-put s <get-var-once line>>
      <stream-put s <pad "" <get-var ws>>>
      <stream-put s <get-var-once class>>
      <stream-put s "\n">
      <foreach l line start=1>
	<set-var l=<word-wrap <get-var-once l> width=60 indent=8>>
	<stream-put s <get-var-once l>>
	<stream-put s "\n">
      </foreach>

      ;;; Put the documentation.
      <stream-put s <word-wrap <get-var-once doc> width=60 indent=4>>
      <stream-put s "\n\n">
    </when>
  </foreach>
</with-open-stream>

<mhc::print "<debugging-output retrieve>">
<mhc::print "<system-error-output retrieve>">
